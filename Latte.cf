-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

ClassDef.  TopDef ::= "class" Ident "{" [Member] "}";

ClassExtend. TopDef ::= "class" Ident "extends" Ident "{" [Member] "}";

separator nonempty TopDef "" ;

Arg. 	   Arg ::= Type Ident;

separator  Arg "," ;

-- classess   ----------------------------------------------

Attr.      Member ::= Type Ident ";" ;

Method.    Member ::= Type Ident "(" [Arg] ")" Block ;

separator nonempty Member "" ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Expr7 "=" Expr  ";" ;

Incr.      Stmt ::= Expr7 "++"  ";" ;

Decr.      Stmt ::= Expr7 "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

For.       Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

ClassT.    Type ::= Ident ;

Arr.       Type ::= Type "[]" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr7 ::= Ident ;

Subs.      ESubs ::= Expr7 "[" Expr"]" ;

ESubs.     Expr7 ::= ESubs;

EApp.      Expr7 ::= Ident "(" [Expr] ")" ;

EVarR.     Expr7 ::= Ident "." Expr7 ;

EAppR.     Expr7 ::= Ident "(" [Expr] ")"  "." Expr7;

ESubsR.    Expr7 ::= ESubs "." Expr7 ;

ENewR.     Expr7 ::= "(" ENew ")" "." Expr7;

ENewTmp.   Expr7 ::= "(" ENew ")" ;

NewArr.    ENew ::= "new" Type "[" Expr "]" ;

NewClass.  ENew ::= "new" Type ;

ENew.      Expr6 ::= ENew ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

ELitNull.  Expr6 ::= "(" Ident ")" "null" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 7 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

